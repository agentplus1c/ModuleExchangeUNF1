
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Расписание") Тогда
		ТекОбъект = РеквизитФормыВЗначение("Объект");
		Если ТекОбъект.СтруктураЕстьСвойства(Параметры.Расписание, "ДниНедели,ВремяНачала,ВремяОкончания,ПериодЗаписи") Тогда
			Расписание = Параметры.Расписание;			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ПеренестиНаФорму();
	
КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПрименить(Команда)

	СтрДни = "";
	Для Сч = 1 По 7 Цикл
		Если ЭтаФорма["День" + Сч] Тогда
			СтрДни = СтрДни + ?(СтрДни = "", "", ",") + Строка(Сч);
		КонецЕсли;
	КонецЦикла;
	
	СтррРезультат = Новый Структура;
	СтррРезультат.Вставить("ДниНедели", 	 СтрДни);
	СтррРезультат.Вставить("ВремяНачала", 	 Формат(ВремяНачала, 	"ДФ='HH:mm'"));
	СтррРезультат.Вставить("ВремяОкончания", Формат(ВремяОкончания, "ДФ='HH:mm'"));
	СтррРезультат.Вставить("ПериодЗаписи", 	 ПериодЗаписи);
	
	Оповестить("АПИзменениеРасписанияВеденияТрека", СтррРезультат, ЭтаФорма);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоУмолчанию(Команда)
	
	Расписание = Новый Структура("ДниНедели,ВремяНачала,ВремяОкончания,ПериодЗаписи",
					"1,2,3,4,5", "09:00", "18:00", 60);
	ПеренестиНаФорму();
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура переносит свойства расписания на форму.
Процедура ПеренестиНаФорму()
	
	Если ТипЗнч(Расписание) = Тип("Структура") Тогда
		
		ВремяНачала 	= ПолучитьДатуВремяИзСтрокиВремени(Расписание.ВремяНачала);
		ВремяОкончания 	= ПолучитьДатуВремяИзСтрокиВремени(Расписание.ВремяОкончания);
		ПериодЗаписи 	= Расписание.ПериодЗаписи;
		
		Для Сч = 1 По 7 Цикл
			СтрСч = Строка(Сч);
			ЭтаФорма["День"+СтрСч] = 0 <> Найти(Расписание.ДниНедели, СтрСч);
		КонецЦикла;
			
	КонецЕсли;
		
	Если ПериодЗаписи = 0 Тогда
		ПериодЗаписи = 60;
	КонецЕсли;
	
КонецПроцедуры

// Функция вовзвращает значение типа "Дата" из строки в формате "HH:MM".
// Используется для вывода на форме в поле типа "Дата" только времени.
Функция ПолучитьДатуВремяИзСтрокиВремени(СтрВремя)
	
	Если ТипЗнч(СтрВремя) <> Тип("Строка") Или СтрДлина(СтрВремя) <> 5 Тогда
		СтрВремя = "00:00";
	КонецЕсли;
	
	Возврат Дата(2016, 07, 22, Число(Лев(СтрВремя, 2)), Число(Прав(СтрВремя, 2)), 0);	
	
КонецФункции

// СлужебныеПроцедурыИФункции
#КонецОбласти
