#Область ГлобальныеПеременные

&НаКлиенте
Перем гМодульК;  // общий клиентский модуль

// ГлобальныеПеременные   
#КонецОбласти


#Область СовместимостьСПлатформой_8_3_5

// Подставляет параметры в строку. Максимально возможное число параметров - 5.
// Параметры в строке задаются как %<номер параметра>. Нумерация параметров начинается с единицы.
//
// Параметры:
//  СтрокаПодстановки  - Строка - шаблон строки с параметрами (вхождениями вида "%ИмяПараметра");
//  Параметр<n>        - Строка - подставляемый параметр.
//
// Возвращаемое значение:
//  Строка   - текстовая строка с подставленными параметрами.
//
&НаКлиентеНаСервереБезКонтекста
Функция СтрШаблон_(СтрокаПодстановки,
	Параметр1, Параметр2 = Неопределено, Параметр3 = Неопределено, Параметр4 = Неопределено, Параметр5 = Неопределено, Параметр6 = Неопределено)
	
	Результат = СтрЗаменить(СтрокаПодстановки, "%1", Параметр1);
	Результат = СтрЗаменить(Результат, "%2", Параметр2);
	Результат = СтрЗаменить(Результат, "%3", Параметр3);
	Результат = СтрЗаменить(Результат, "%4", Параметр4);
	Результат = СтрЗаменить(Результат, "%5", Параметр5);
	Результат = СтрЗаменить(Результат, "%6", Параметр6); // sk_191129 Добавлено в рамках (MOD-926)
	
	Возврат Результат;
	
КонецФункции
	
// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//     - для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//     Е если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//  СокращатьНепечатаемыеСимволы - Булево - сокращать непечатаемые символы по краям каждой из найденных подстрок.
//
// Возвращаемое значение:
//  Массив - массив строк.
&НаКлиентеНаСервереБезКонтекста 
Функция СтрРазделить_(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь)
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

///  Объединяет строки из массива в строку с разделителями.
//
// Параметры:
//  Массив      - Массив - массив строк которые необходимо объединить в одну строку;
//  Разделитель - Строка - любой набор символов, который будет использован в качестве разделителя.
//
// Возвращаемое значение:
//  Строка - строка с разделителями.
// 
&НаКлиентеНаСервереБезКонтекста 
Функция СтрСоединить_(Массив, Разделитель = ",", СокращатьНепечатаемыеСимволы = Ложь)

	Результат = "";
	ТекРазделитель = "";
	
	Для Индекс = 0 По Массив.ВГраница() Цикл
		
		Подстрока = Массив[Индекс];
		
		Если СокращатьНепечатаемыеСимволы Тогда
			Подстрока = СокрЛП(Подстрока);
		КонецЕсли;
		
		Если ТипЗнч(Подстрока) <> Тип("Строка") Тогда
			Подстрока = Строка(Подстрока);
		КонецЕсли;
		
		Результат = Результат + ТекРазделитель + Подстрока;
		
		Если Индекс = 0 Тогда
			ТекРазделитель = Разделитель;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// СовместимостьСПлатформой_8_3_5
#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ТекОбъект = РеквизитФормыВЗначение("Объект");	
	
	СтррКонтекст = Новый Структура("СтатусыСправочников,СтатусНастройкиОбмена,ОткрытиеПоНавигации,ЗакрытьФорму,Местоположение,ИнтерфейсТакси,ИсправитьРазмерыЭлементов,ВремяМестоположения,Конфигурация");
	
	//СтррКонтекст.Вставить("ВерсияОбработкиУНФ", ТекОбъект.ПолучитьВерсияОбработкиУНФ()); //vd_180507 Добавлена версия обработки УНФ //vd_181022 Версия берется из методанных обработки
		
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры); 
	СтррКонтекст.Местоположение 	 = ТекОбъект.ПрочитатьЗначениеНастройки("МестоположениеДляИнформера"); // местоположение пользователя для виджета информера главного окна
	СтррКонтекст.ВремяМестоположения = ТекОбъект.ПрочитатьЗначениеНастройки("ВремяМестоположения"); // время определения последнего местоположения
	
	//++УНФ
	// признаки, что автоматически были отключены настройки "ВыгружатьХарактеристикиНоменклатуры", "ИспользоватьКонтрагентов", "ИспользоватьСоглашения"
//	СтррКонтекст.Вставить("ОтключенаНастройкаВыгружатьХарактеристикиНоменклатуры", 	Ложь);
//	СтррКонтекст.Вставить("ОтключенаНастройкаИспользоватьКонтрагентов", 			Ложь);
//	СтррКонтекст.Вставить("ОтключенаНастройкаИспользоватьСоглашения", 				Ложь);
//	
//	// считываем настройки конфигурации для сравнения с настройками обмена модуля.
//	
////ИспользоватьХарактеристикиНоменклатуры 	= Константы.ИспользоватьХарактеристикиНоменклатуры.Получить();
//	ИспользоватьПартнеровИКонтрагентов		= Константы.ИспользоватьПартнеровИКонтрагентов.Получить();
//	ИспользоватьСоглашенияСКлиентами		= Константы.ИспользованиеСоглашенийСКлиентами.Получить();
//	ИспользоватьДоговорыСКлиентами			= Константы.ИспользоватьДоговорыСКлиентами.Получить();
		
	//
	//СтррЗначенияНастроек = ТекОбъект.ПрочитатьЗначенияНастроек("ИспользоватьХарактеристики,ИспользоватьКонтрагентов,ИспользоватьСоглашения", Ложь);
	//
	//Если НЕ ИспользоватьХарактеристикиНоменклатуры И СтррЗначенияНастроек.ИспользоватьХарактеристики Тогда 
	//	ТекОбъект.СохранитьЗначениеНастройки("ИспользоватьХарактеристики", Ложь); // отключаем настройку "ВыгружатьХарактеристикиНоменклатуры"
	//	СтррКонтекст.ОтключенаНастройкаВыгружатьХарактеристикиНоменклатуры = Истина;
	//КонецЕсли;
	//
	//Если НЕ ИспользоватьПартнеровИКонтрагентов И СтррЗначенияНастроек.ИспользоватьКонтрагентов Тогда 
	//	ТекОбъект.СохранитьЗначениеНастройки("ИспользоватьКонтрагентов", Ложь); // отключаем настройку "ИспользоватьКонтрагентов"
	//	СтррКонтекст.ОтключенаНастройкаИспользоватьКонтрагентов = Истина;
	//КонецЕсли;
	//
	////++УНФ
	////Если НЕ ИспользоватьДоговорыСКлиентами И ИспользоватьСоглашенияСКлиентами = Перечисления.ИспользованиеСоглашенийСКлиентами.НеИспользовать И СтррЗначенияНастроек.ИспользоватьСоглашения Тогда 
	////	ТекОбъект.СохранитьЗначениеНастройки("ИспользоватьСоглашения", Ложь); // отключаем настройку "ИспользоватьСоглашения"
	////	СтррКонтекст.ОтключенаНастройкаИспользоватьСоглашения = Истина;
	////КонецЕсли;
	//	ТекОбъект.СохранитьЗначениеНастройки("ИспользоватьСоглашения", Ложь); // отключаем настройку "ИспользоватьСоглашения"
	//	СтррКонтекст.ОтключенаНастройкаИспользоватьСоглашения = Истина;
	////--
	//
	//--УНФ
	
	//vd_180226 проверка версии
	
	стррВерсияУчетнойСистемы = ТекОбъект.ОприделитьВерсиюУНФ();
	Если стррВерсияУчетнойСистемы.КонфигурацияВерсия <> "1.6" Тогда
		ТекОбъект.ОповеститьОСобытии(НСтр("ru = 'Несоответствие версии УНФ! Обработка разработана для учетной системы УНФ 1.6'; uk = 'Невідповідність версії УНФ! Обробка розроблена для облікової системи УНФ 1.6'"));
		Отказ = Истина;
	КонецЕсли;
	
	СтррКонтекст.ЗакрытьФорму = Ложь; // используется для запрета запуска второго экземпляра обработки
	
	Если СтррКонтекст.ВХОбщиеПараметры = Неопределено Тогда
		СтррПараметры = Новый Структура("КлючСеанса", Новый УникальныйИдентификатор);
		СтррКонтекст.ВХОбщиеПараметры = ПоместитьВоВременноеХранилище(СтррПараметры);
		СтррКонтекст.ОткрытиеПоНавигации = Ложь;
	Иначе // значит форма открыта из другой формы обработки обмена данными по команде навигации
		СтррКонтекст.ОткрытиеПоНавигации = Истина;
	КонецЕсли; 
	
	СтррКонтекст.ИнтерфейсТакси = ТекОбъект.КонфигурацияРазмерностьЭлементовФормыДляТакси() 
		И ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси;
		
	СтррКонтекст.ИсправитьРазмерыЭлементов = ТекОбъект.КонфигурацияРежимСовместимости("<=8.3.6") 
		И ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси;
		
	СтррКонтекст.Вставить("АдресСтраницыПриветствия", "agentplus.online");
	СтррКонтекст.Вставить("КлиентМетрикаИнформера", ТекОбъект.ПрочитатьЗначениеНастройки("КлиентМетрика")); // sk_191129 Добавлена в рамках (MOD-926)
	
	СправочникиПрочитатьИзНастроекГотовностьКОбмену();
	НастройкиОбменаПрочитатьИзНастроекГотовностьКОбмену();
	ПрочестьНастройкиСервер();
	
	ЗаполнитьРазделы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если СтррКонтекст.ЗакрытьФорму Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	//vd_181022 Версия берется из методанных обработки
	//ЭтаФорма.Заголовок = "Агент Плюс: Мобильная торговля Базовая (ver. " + СтррКонтекст.Версия + "." + Строка(СтррКонтекст.ВерсияОбработкиУНФ) + ")";//vd_180507 Добавлена версия УНФ
	ЭтаФорма.Заголовок = СтрШаблон_(НСтр("ru='Агент Плюс: Мобильная торговля Базовая (ver. %1)';uk='Агент Плюс: Мобільна торгівля Базова (ver.%1)'"),СтррКонтекст.Версия);
	
	Цвета = СтррКонтекст.Цвета;
	Элементы.ГруппаРазделы.ЦветФона 		= Цвета.ПолеГлавноеОкно;
	Элементы.ГруппаКоманднаяПанель.ЦветФона = Цвета.ФонРаздела;	
	Элементы.Разделы.ЦветФона  				= Цвета.ПолеГлавноеОкно;
	Элементы.Разделы.ЦветРамки 				= Цвета.ПолеГлавноеОкно;
	
	ЕстьЗамечания = ОбновитьСтатусГотовности(Ложь);
	
	ПодключитьОбработчикОжидания("ОбновитьСтатусГотовностиПоТаймеру", 3600, Ложь);
	
	ИзмениласьВерсияОбработки = ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Объект.ВерсияНастроек, СтррКонтекст.Версия) < 0;
	
	Если ИзмениласьВерсияОбработки Тогда 
		// изменилась версия обработки, данные в хранилище от старой версии
		
		Модуль	= ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульОбновлений", СтррКонтекст, ЭтаФорма, "АгентПлюсМодульОбновленийКлиент");
		Текст = Модуль.ИнициализироватьДанныеВХранилищеДляНовойВерсииКлиент(Объект.ВерсияНастроек, СтррКонтекст.Версия); 
		
		Если Не ПустаяСтрока(Текст) Тогда
			ОчиститьСообщения();
			Сообщить(Текст);
		КонецЕсли; 
		
		ПрочестьНастройкиСервер(); 		
		
	КонецЕсли;
	
	// отменяем автозапуск обмена, если изменилась версия обработки - на всякий случай, чтобы пользователь увидел сообщения об изменениях.	
	Если Объект.АвтозапускОбменаДанными И Не ЕстьЗамечания И Не ИзмениласьВерсияОбработки И Не СтррКонтекст.ОткрытиеПоНавигации Тогда
		Элементы.ДекорацияЗапускОбмена.Видимость = Истина;
		ТаймерЗапускаОбмена = 6; // таймаут на 5 секунд - секунда отнимется при прямом вызове обработчика ПоказатьОбменДаннымиПоТаймеру()
		ПоказатьОбменДаннымиПоТаймеру();
		ПодключитьОбработчикОжидания("ПоказатьОбменДаннымиПоТаймеру", 1, Ложь);
	Иначе
		Элементы.ДекорацияЗапускОбмена.Видимость = Ложь;
	КонецЕсли;
	
	Если Не СтррКонтекст.ОткрытиеПоНавигации Тогда
		Оповестить("АППроверкаУникальностиЗапускаОбработкиОбмена", СтррКонтекст.ВХОбщиеПараметры);	
	КонецЕсли; 
	
	Если Не СтррКонтекст.ИнтерфейсТакси Тогда
		Элементы.СтрокаБраузера.Ширина   = Элементы.СтрокаБраузера.Ширина * 2.6;
		Элементы.БыстраяНастройка.Ширина = 19;
		Элементы.БыстраяНастройка.Высота = 2;
	КонецЕсли; 
	
	Если СтррКонтекст.ИсправитьРазмерыЭлементов	Тогда
		
		Элементы.НастройкаМодуляОбменаРасширеннаяПодсказка.Высота = 2;
		Элементы.НастройкиАгентовРасширеннаяПодсказка.Высота = 3;
		
		// Раздел "Справочники"
		Элементы.СправочникСпискиТорговыхТочекРасширеннаяПодсказка.Высота 	= 3;
		Элементы.СправочникКатегорииДокументовРасширеннаяПодсказка.Высота 	= 3;
		Элементы.СправочникСтатусыКонтрагентовРасширеннаяПодсказка.Высота 	= 3;
		Элементы.СправочникРезультатыПосещенийРасширеннаяПодсказка.Высота 	= 3;
		
		// Раздел "Документы"
		Элементы.ДокументыПосещенияРасширеннаяПодсказка.Высота 		= 2;
		Элементы.ДокументыМерчендайзингРасширеннаяПодсказка.Высота 	= 3;
		
		// Раздел "Планирование и контроль"
		Элементы.СправочникСпискиТорговыхТочекПланированиеРасширеннаяПодсказка.Высота = 3;
		Элементы.ВыполнениеПланаПосещенийРасширеннаяПодсказка.Высота = 2;
		Элементы.ЗаполнитьКоординатыКонтрагентовРасширеннаяПодсказка.Высота = 2;
		
		// Раздел "Сервис"
		Элементы.НастройкаМодуляОбменаСервисРасширеннаяПодсказка.Высота = 2;
		Элементы.ЖурналОбменаДаннымиРасширеннаяПодсказка.Высота = 2;
		Элементы.БыстраяНастройкаВСервисеРасширеннаяПодсказка.Высота = 2;
		
		Элементы.СкрыватьБыструюНастройкуСервис.Заголовок = НСтр("ru='Скрывать ""Быструю настройку""';uk='Приховувати ""Швидке налаштування""'");
		
	КонецЕсли; 
	// В информационное поле выводятся предупреждения для пользователя, в том случае когда настройки обмена могут конфликтовать с настройками конфигурации УС.
	Элементы.ИнформационноеПоле.Видимость = Ложь;
	
	ИтоговаяСтрока 	= Новый Массив;
	ШрифтОбычный	= Новый Шрифт(,8);
	ШрифтПолуЖирный = Новый Шрифт(,8, Истина);
	ПереводСтроки	= Новый ФорматированнаяСтрока(Символы.ПС);
	
	ИнформационноеПоле = Новый ФорматированнаяСтрока(ИтоговаяСтрока);

	//vd_181001
	Если СтррКонтекст.Конфигурация = "УНФ_UA" Тогда
		Элементы.ВидеоКаналАгентПлюс.Заголовок = НСтр("ru = 'Видео-канал ""Агент Софт""'; uk = 'Відео-канал ""Агент Софт""'");
		Элементы.ВидеоКаналАгентПлюс.РасширеннаяПодсказка.Заголовок = НСтр("ru = 'Канал ""Агент Софт"" на сайте youtube.'; uk = 'Канал ""Агент Софт"" на сайті youtube.'");
	КонецЕсли;
	
	//УНФ
	//Если СтррКонтекст.ОтключенаНастройкаВыгружатьХарактеристикиНоменклатуры Тогда
	//	
	//	Элементы.ИнформационноеПоле.Видимость = Истина;
	//	Элементы.ИнформационноеПоле.Высота = 7;
	//	
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Предупреждение: ';uk='Попередження:'"), ШрифтПолуЖирный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Автоматически выключена настройка - ""Выгружать характеристики номенклатуры"" (перейти на закладку Обмен данными - Общие настройки - ';uk='Автоматично виключена налаштування - ""Вивантажувати характеристики номенклатури"" (перейти на закладку Обмін даними - Загальні налаштування -'"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Основные';uk='Основні'"), ШрифтОбычный,,,"ПерейтиНаЗакладкуОсновныеХарактеристикиНоменклатуры")); // ссылка и ее строковое представление для обработчика "ИнформационноеПолеОбработкаНавигационнойСсылки"
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='). ';uk='). '"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(ПереводСтроки);
	//	
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Причина: в настройках конфигурации выключена функциональная опция ""Использовать характеристики номенклатуры"" (перейти в раздел НСИ и Администрирование - Настройка НСИ - ';uk='Причина: в налаштуваннях конфігурації виключена функціональна опція ""Використовувати характеристики номенклатури"" (перейти в розділ НДІ і Адміністрація - Налаштування НДІ -'"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Номенклатура';uk='Номенклатура'"), ШрифтОбычный,,,"НастройкиНоменклатуры")); // ссылка и ее строковое представление для обработчика "ИнформационноеПолеОбработкаНавигационнойСсылки"
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=').';uk=').'"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(ПереводСтроки);
	//	
	//КонецЕсли;

	
	//Если ИспользоватьКонтрагентов Тогда
	//	
	//	Элементы.ИнформационноеПоле.Видимость = Истина;
	//	Элементы.ИнформационноеПоле.Высота = Элементы.ИнформационноеПоле.Высота + 7;
	//	
	//	Если ИтоговаяСтрока.Количество() > 0 Тогда 
	//		ИтоговаяСтрока.Добавить(ПереводСтроки);	
	//	КонецЕсли;
	//	
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Предупреждение: ';uk='Попередження:'"), ШрифтПолуЖирный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Автоматически выключена настройка - ""Использовать контрагентов"" (перейти на закладку Обмен данными - Общие настройки - ';uk='Автоматично виключена налаштування - ""Використовувати контрагентів"" (перейти на закладку Обмін даними - Загальні налаштування -'"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Основные';uk='Основні'"), ШрифтОбычный,,,"ПерейтиНаЗакладкуОсновныеИспользоватьКонтрагентов"));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='). ';uk='). '"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(ПереводСтроки);
	//	
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Причина: в настройках конфигурации выключена функциональная опция ""Независимо вести партнеров и контрагентов"" (перейти в раздел НСИ и Администрирование - Настройка НСИ - ';uk='Причина: в налаштуваннях конфігурації виключена функціональна опція ""Незалежно вести партнерів і контрагентів"" (перейти в розділ НДІ і Адміністрація - Налаштування НДІ -'"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='CRM и маркетинг';uk='CRM і маркетинг'"), ШрифтОбычный,,,"CRMИМаркетинг"));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=').';uk=').'"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(ПереводСтроки);
	//	
	//КонецЕсли;
	
	//УНФ
	//Если СтррКонтекст.ОтключенаНастройкаИспользоватьСоглашения Тогда
	//	
	//	Элементы.ИнформационноеПоле.Видимость = Истина;
	//	Элементы.ИнформационноеПоле.Высота = Элементы.ИнформационноеПоле.Высота + 7;
	//	
	//	Если ИтоговаяСтрока.Количество() > 0 Тогда 
	//		ИтоговаяСтрока.Добавить(ПереводСтроки);	
	//	КонецЕсли;

	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Предупреждение: ';uk='Попередження:'"), ШрифтПолуЖирный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Автоматически выключена настройка - ""Использовать соглашения"" (Обмен данными - Общие настройки - ';uk='Автоматично виключена налаштування - ""Використовувати угоди"" (Обмін даними - Загальні налаштування -'"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Основные';uk='Основні'"), ШрифтОбычный,,,"ПерейтиНаЗакладкуОсновныеИспользоватьСоглашения"));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='). ';uk='). '"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(ПереводСтроки);
	//	
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Причина: в настройках конфигурации выключена функциональная опция ""Использование соглашений с клиентами"" (перейти в раздел НСИ и Администрирование - Настройка НСИ - ';uk='Причина: в налаштуваннях конфігурації виключена функціональна опція ""Використання угод з клієнтами"" (перейти в розділ НДІ і Адміністрація - Налаштування НДІ -'"), ШрифтОбычный));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Продажи';uk='Продажі'"), ШрифтОбычный,,,"Продажи"));
	//	ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=').';uk=').'"), ШрифтОбычный));
	//	
	//КонецЕсли;
	//--УНФ
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "АПНастройкиПараметровОбменаИзмененияЗаписаны" Тогда
		
		ПрочестьВсеНастройкиИОбновитьСтатус();
		
	ИначеЕсли ИмяСобытия = "АПНастройкиАгентовИзмененияЗаписаны" Тогда
		
		ОбновитьСтатусГотовности(Истина);
		
	ИначеЕсли ИмяСобытия = "АПМобильныеУстройстваИзменениеСпискаМУ" Тогда
		
		ОбновитьСтатусГотовности(Истина);
		
	ИначеЕсли ИмяСобытия = "АПНастройкиСброшены" Тогда
		
		Закрыть();
		
	ИначеЕсли ИмяСобытия = "АППроверкаУникальностиЗапускаОбработкиОбмена" Тогда
		
		Если Параметр <> СтррКонтекст.ВХОбщиеПараметры Тогда // второй экземпляр обработки справшивает - уже открыта обработка или нет
			Оповестить("АПЗакрытьОбработкуОбменДанными", Параметр); // шлем событие закрытия обработки с конкретным ключем сеанса
		КонецЕсли; 
		
	ИначеЕсли ИмяСобытия = "АПЗакрытьОбработкуОбменДанными" И Параметр = СтррКонтекст.ВХОбщиеПараметры Тогда
		
		Состояние(НСтр("ru = 'Обработка обмена данными уже запущена.'; uk = 'Обробка обміну даними вже запущена.'"));
		СтррКонтекст.ЗакрытьФорму = Истина;
		Если ЭтаФорма.Открыта() Тогда
			Закрыть();		
		КонецЕсли;  
		
	КонецЕсли;

КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	МодульК().КомандаВыполнить(Команда, ЭтотОбъект)
	

КонецПроцедуры

&НаКлиенте
Процедура КомандаСправка(Команда)

	РазделСправки = Неопределено;
	
	СтрокаТ = Элементы.Разделы.ТекущиеДанные;
	Если СтрокаТ <> Неопределено Тогда
		Если СтрокаТ.Раздел = "Главная" Тогда // если находимся на "Домашней странице", то показываем "Общий раздел" справки.
			РазделСправки = ЭтаФорма.ИмяФормы;
		Иначе
			РазделСправки = ЭтаФорма.ИмяФормы + "_" + СтрокаТ.Раздел;
		КонецЕсли; 
	КонецЕсли;
	
	МодульК().КомандаСправка(РазделСправки);
	
КонецПроцедуры

#Область ОбработчикиКомандФормы_Обработки

&НаКлиенте
Процедура КомандаНастройкиОбщие(Команда)
	
	ОткрытьНастройкиОбмена(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура КомандаБыстраяНастройка(Команда)
	
	Текст = НСтр("ru='Запустить быструю настройку модуля обмена данными?';uk='Запустити швидке налаштування модуля обміну даними?'");
	Оповещение = Новый ОписаниеОповещения("КомандаБыстраяНастройкаПродолжить", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаБыстраяНастройкаПродолжить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		МодульК().ОткрытьФормуОбработки("БыстраяНастройка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОбменДаннымиПоТаймеру()
	
	ТаймерЗапускаОбмена = ТаймерЗапускаОбмена - 1;
	Текст = НСтр("ru='Автозапуск обмена данными через %1 сек.';uk='Автозапуск обміну даними через %1 сек.'");
	Элементы.АвтозапускОбменаДанными.Заголовок = СтрШаблон_(Текст, ТаймерЗапускаОбмена);
	Если ТаймерЗапускаОбмена = 0 Тогда
		Элементы.ДекорацияЗапускОбмена.Видимость = Ложь;	
		Если Объект.АвтозапускОбменаДанными Тогда // флажок пользователь мог снять сразу после открытия формы
			МодульК().КомандаВыполнить("ПоказатьОбменДанными");
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// ОбработчикиКомандФормы_Обработки
#КонецОбласти

#Область ОбработчикиКомандФормы_Виджет

&НаКлиенте
Процедура КомандаУказатьМестоположение(Команда)
	
	стррПараметры = Новый Структура("Местоположение", СтррКонтекст.Местоположение);
	Оповещение = Новый ОписаниеОповещения("КомандаУказатьМестоположениеПродолжить", ЭтотОбъект);
	МодульК().ОткрытьФормуОбработки("ВыборМестоположения", СтррПараметры,, Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура КомандаУказатьМестоположениеПродолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			СтррКонтекст.Местоположение = Результат;
			СтрокаБраузера = ПолучитьАдресСтраницыПриветствия();
			СохранитьЗначениеНастройки("МестоположениеДляИнформера", СтррКонтекст.Местоположение);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейти(Команда)
	
	Меню = Новый СписокЗначений;
	
	МодульКарты = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульКарты", СтррКонтекст);
	КодСтраны = МодульКарты.КодСтраныМестоположения(СтррКонтекст.Местоположение);
	Если КодСтраны = "UA" Тогда
		Меню.Добавить("Сайт_ua", "www.agentplus.com.ua",, Элементы.ДекорацияАгентПлюс.Картинка);
	Иначе
		Меню.Добавить("Сайт_ru", "www.agentplus.ru",, 	  Элементы.ДекорацияАгентПлюс.Картинка);	
	КонецЕсли; 
	Меню.Добавить("ЛичныйКабинет", НСтр("ru='Личный кабинет';uk='Особистий кабінет'"),, Элементы.ДекорацияЛК.Картинка);
	Оповещение = Новый ОписаниеОповещения("КомандаПерейтиПродолжить", ЭтотОбъект);	
	ПоказатьВыборИзМеню(Оповещение, Меню, Элементы.Перейти);

КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиПродолжить(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		Если Результат.Значение = "Сайт_ru" Тогда
			ЗапуститьПриложение("http://www.agentplus.ru/");
		ИначеЕсли Результат.Значение = "Сайт_ua" Тогда
			ЗапуститьПриложение("http://www.agentplus.com.ua/");
		ИначеЕсли Результат.Значение = "ЛичныйКабинет" Тогда
			ЗапуститьПриложение("https://agentplus.online/");
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

// ОбработчикиКомандФормы_Виджет
#КонецОбласти 

#Область ОбработчикиКомандФормы_Видео

&НаКлиенте
Процедура КомандаПоказатьВидео(Команда)
	
	ствВидео = Новый Соответствие;
	Если СтррКонтекст.Конфигурация = "УНФ_UA" Тогда
		ствВидео.Вставить("ВидеоНастройкиМодуляГлавная", 	"https://www.youtube.com/watch?v=cLJoMHTwzP4");	
		ствВидео.Вставить("ВидеоКаналАгентПлюс", 			"https://www.youtube.com/channel/UCaNHobGKp8TRgeAN_-O-iDg");
		ствВидео.Вставить("ВидеоНачальнаяНастройкаМодуля", 	"https://www.youtube.com/watch?v=cLJoMHTwzP4");
		ствВидео.Вставить("ВидеоМТРелиз20", 				"https://www.youtube.com/channel/UCaNHobGKp8TRgeAN_-O-iDg");
	Иначе
		ствВидео.Вставить("ВидеоНастройкиМодуляГлавная", 	"https://youtu.be/AzV0kxY8IMw");	
		ствВидео.Вставить("ВидеоКаналАгентПлюс", 			"https://www.youtube.com/user/agentplusinc");
		ствВидео.Вставить("ВидеоНачальнаяНастройкаМодуля", 	"https://youtu.be/AzV0kxY8IMw");
		ствВидео.Вставить("ВидеоМТРелиз20", 				"https://youtu.be/9xuXBQDa6Yg");
	КонецЕсли;

	ИмяЭлемента = ЭтаФорма.ТекущийЭлемент.Имя;
	Если ствВидео.Получить(ИмяЭлемента) <> Неопределено Тогда
	    ЗапуститьПриложение(ствВидео.Получить(ИмяЭлемента)); 
	Иначе
		ВызватьИсключение(СтрШаблон_(НСтр("ru='Неизвестный элемент формы для запуска видео: %1';uk='Невідомий елемент форми для запуску відео: %1'"),
										ИмяЭлемента));
	КонецЕсли; 
	
КонецПроцедуры

// ОбработчикиКомандФормы_Видео
#КонецОбласти

&НаКлиенте
Процедура КомандаТест(Команда)
	
	//Оповестить("АПНастройкиСброшены", Неопределено);
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НадписьСтатусНажатие(Элемент)
	
	Если Элемент.Имя = "НадписьГотовностьАгентов" Тогда
		МодульК().КомандаВыполнить("ПоказатьНастройкиАгентов");
		Оповестить("АПНастройкиАгентов_Оповещение", Новый Структура("ВыделитьПервогоАгентаСОшибкой", Истина));
	ИначеЕсли Элемент.Имя = "НадписьГотовностьМУ" Тогда
		МодульК().КомандаВыполнить("ПоказатьМобильныеУстройства");
	ИначеЕсли Элемент.Имя = "НадписьГотовностьНастроекОбмена" Тогда
		ОткрытьНастройкиОбмена(Истина);		
	ИначеЕсли Элемент.Имя = "НадписьОбменДанными" Тогда
		МодульК().КомандаВыполнить("ПоказатьОбменДанными");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	СтрокаТ = Элемент.ТекущиеДанные;
	Если СтрокаТ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда 
		Если СтрокаТ.ИконкаРаздела < 9 Тогда // подменяем иконку текущего раздела на белую
			СтрокаТ.ИконкаРаздела = СтрокаТ.ИконкаРаздела + 9;
		КонецЕсли;
		Для каждого стзТабРазделы Из тзРазделы Цикл
			Если стзТабРазделы <> СтрокаТ Тогда
			    Если стзТабРазделы.ИконкаРаздела > 9 Тогда
					стзТабРазделы.ИконкаРаздела = стзТабРазделы.ИконкаРаздела - 9;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	ПереключитьСтраницу(СтрокаТ.Раздел);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаБраузераПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Если ДанныеСобытия.Anchor <> Неопределено И ЗначениеЗаполнено(ДанныеСобытия.Anchor.href) Тогда
		СтандартнаяОбработка = Ложь;
		ЗапуститьПриложение(ДанныеСобытия.Anchor.href);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СкрыватьБыструюНастройкуПриИзменении(Элемент)
	
	СохранитьНастройкуСервер("СкрыватьБыструюНастройку");
	Если Объект.СкрыватьБыструюНастройку Тогда // если изменен флажок на домашней странице, его не скрываем
	    ОчиститьСообщения();
		Сообщить(НСтр("ru='Изменения вступят в силу после перезапуска обработки.';uk='Зміни вступлять в силу після перезапуску обробки.'"));
	Иначе
		ОбновитьСтатусГотовности(Ложь);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АвтозапускОбменаДаннымиПриИзменении(Элемент)
	
	Элементы.АвтозапускОбменаДанными.Заголовок = НСтр("ru='Автозапуск обмена данными';uk='Автозапуск обміну даними'");
	ОтключитьОбработчикОжидания("ПоказатьОбменДаннымиПоТаймеру");
	Элементы.ДекорацияЗапускОбмена.Видимость = Ложь;
	СохранитьНастройкуСервер("АвтозапускОбменаДанными");
	
	Если Объект.АвтозапускОбменаДанными Тогда
		Текст = НСтр("ru='Запустить обмен данными с мобильными устройствами?';uk='Запустити обмін даними з мобільними пристроями?'");
		Оповещение = Новый ОписаниеОповещения("АвтозапускОбменаДаннымиЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АвтозапускОбменаДаннымиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		МодульК().КомандаВыполнить("ПоказатьОбменДанными");
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

// ОбработчикиСобытийЭлементовФормы
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_Прочие

&НаКлиенте
Функция МодульК()

	Если гМодульК = Неопределено Тогда
	    гМодульК = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульОбщий", СтррКонтекст);
	КонецЕсли; 
	
	Возврат гМодульК;

КонецФункции 

&НаКлиенте
Процедура ОткрытьНастройкиОбмена(УчестьСтатусНастроек)
	
	МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
	
	Если УчестьСтатусНастроек Тогда
		стррСтатус = СтррКонтекст.СтатусНастройкиОбмена;
		Если Не стррСтатус.Готов Тогда
			Если 0 <> Найти(стррСтатус.КодСостояния, "Каталог") Тогда
				стррПараметры = Новый Структура("Закладка,Сообщение", "ГруппаКаталоги", "УказатьКаталог");
				Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", стррПараметры);
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КодироватьСтрокуСервер(Стр)
	Возврат КодироватьСтроку(Стр, СпособКодированияСтроки.URLВКодировкеURL);
КонецФункции

// СлужебныеПроцедурыИФункции_Прочие
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Форма

&НаКлиенте
Процедура ПереключитьСтраницу(ИмяСтраницы)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["Группа" + ИмяСтраницы];
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьРазделы()
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 1; // 2 - иконка домика с восклицательным знаком
	СтрокаТ.ПредставлениеРаздела = НСтр("ru='Домашняя страница';uk='Домашня сторінка'");
	СтрокаТ.Раздел 				 = "Главная";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 3;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru='Обмен данными';uk='Обмін даними'");
	СтрокаТ.Раздел 				 = "ОбменДанными";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 4;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru='Справочники';uk='Довідники'");
	СтрокаТ.Раздел 				 = "Справочники";
	
	СтрокаТ = тзРазделы.Добавить(); // sd_23082017
	СтрокаТ.ИконкаРаздела 		 = 5;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru='Документы';uk='Документи'");
	СтрокаТ.Раздел 				 = "Документы";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 6;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru='Планирование и контроль';uk='Планування і контроль'");
	СтрокаТ.Раздел 				 = "ПланированиеИКонтроль";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 7;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru='Сервис';uk='Сервіс'");
	СтрокаТ.Раздел 				 = "Сервис";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 8;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru='Помощь';uk='Допомога'");
	СтрокаТ.Раздел 				 = "Помощь";
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_Форма
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_ХранилищеНастроек

&НаСервере
Процедура ПрочестьНастройкиСервер()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.ВосстановитьЗначенияНастроекОбработки(НастройкиФормы());
	ЗначениеВРеквизитФормы(ТекОбъект, "Объект");

КонецПроцедуры

&НаСервере
Функция НастройкиФормы()

	Возврат "ВерсияНастроек,АвтозапускОбменаДанными,СкрыватьБыструюНастройку";
	
КонецФункции // НастройкиФормы()

&НаСервере
Процедура СохранитьЗначениеНастройки(ИмяНастройки, Значение)
	РеквизитФормыВЗначение("Объект").СохранитьЗначениеНастройки(ИмяНастройки, Значение);
КонецПроцедуры

&НаСервере
Процедура СправочникиПрочитатьИзНастроекГотовностьКОбмену()
	
	СтррКонтекст.СтатусыСправочников 
		= РеквизитФормыВЗначение("Объект").СправочникиГотовыКРаботе("НастройкиАгентов,МобильныеУстройства");
	
КонецПроцедуры
	
&НаСервере
Процедура НастройкиОбменаПрочитатьИзНастроекГотовностьКОбмену()
	
	СтррКонтекст.СтатусНастройкиОбмена = РеквизитФормыВЗначение("Объект").НастройкиОбменаГотовыКРаботе();
	
КонецПроцедуры

&НаСервере
Процедура ПрочестьСписокАгентовИзНастроек()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.ВосстановитьЗначенияНастроекОбработки("НастройкиАгентов");
	ЗначениеВРеквизитФормы(ТекОбъект, "Объект");

КонецПроцедуры

&НаКлиенте
Процедура ПрочестьВсеНастройкиИОбновитьСтатус()
	ПрочестьНастройкиСервер();
	НастройкиОбменаПрочитатьИзНастроекГотовностьКОбмену();
	ОбновитьСтатусГотовности(Ложь);
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуСервер(ИмяНастройки)
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.СохранитьЗначенияНастроекОбработки(ИмяНастройки); 
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_ХранилищеНастроек
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Статусы

// Функция проверяет готовность модуля к работе (готовность настроек модуля) и выводит
// на форме информацию о его готовности.
&НаКлиенте
Функция ОбновитьСтатусГотовности(ПрочитатьИзНастроек)
	
	Если ПрочитатьИзНастроек Тогда
		СправочникиПрочитатьИзНастроекГотовностьКОбмену();
		НастройкиОбменаПрочитатьИзНастроекГотовностьКОбмену();
	КонецЕсли;
	
	ЕстьЗамечания = Ложь;
	Элементы.ДекорацияПриветствие.Заголовок = ПолучитьПриветствие();
	СтрокаБраузера = ПолучитьАдресСтраницыПриветствия();
	
	СтррСтатус = СтррКонтекст.СтатусыСправочников.НастройкиАгентов;
	Если СтррСтатус.Готов Тогда
		Элементы.НадписьГотовностьАгентов.Видимость = Ложь;
	Иначе
		ЕстьЗамечания = Истина;
		Элементы.НадписьГотовностьАгентов.Видимость = Истина;
		Элементы.НадписьГотовностьАгентов.Заголовок = СтррСтатус.Описание;
	КонецЕсли;
	
	СтррСтатус = СтррКонтекст.СтатусыСправочников.МобильныеУстройства;
	Если СтррСтатус.Готов Тогда
		Элементы.НадписьГотовностьМУ.Видимость = Ложь;
	Иначе
		ЕстьЗамечания = Истина;
		Элементы.НадписьГотовностьМУ.Видимость = Истина;
		Элементы.НадписьГотовностьМУ.Заголовок = СтррСтатус.Описание;
	КонецЕсли;
	
	СтррСтатус = СтррКонтекст.СтатусНастройкиОбмена;
	Если СтррСтатус.Готов Тогда
		Элементы.НадписьГотовностьНастроекОбмена.Видимость = Ложь;
	Иначе
		ЕстьЗамечания = Истина;
		Элементы.НадписьГотовностьНастроекОбмена.Видимость = Истина;
		Элементы.НадписьГотовностьНастроекОбмена.Заголовок = СтррСтатус.Описание;
	КонецЕсли;
	
	Элементы.ДекорацияСтатус.Заголовок = ?(ЕстьЗамечания, 
				НСтр("ru='Пожалуйста, примите меры:';uk='Будь ласка, прийміть заходи:'"), 
				НСтр("ru='Все готово к работе. Замечаний нет.';uk='Все готово до роботи. Зауважень немає.'"));
				
	Элементы.ДекорацияЗамечанияОтступ.Видимость	   = ЕстьЗамечания;					
	Элементы.ДекорацияЗамечанияОтступ1.Видимость   = ЕстьЗамечания;
	
	ВидимостьБыстройНастройки = Не Объект.СкрыватьБыструюНастройку И ЕстьЗамечания;	
	Элементы.ДекорацияБыстраяНастройка.Видимость   = ВидимостьБыстройНастройки;
	Элементы.ГруппаБыстраяНастрокаКнопка.Видимость = ВидимостьБыстройНастройки;
				
	Элементы.НадписьОбменДанными.Видимость = Не ЕстьЗамечания;
	Элементы.АвтозапускОбменаДанными.Видимость = Не ЕстьЗамечания;
	
	тзРазделы[0].ИконкаРаздела = ?(ЕстьЗамечания, 2, 1);
	
	Возврат ЕстьЗамечания;

КонецФункции

&НаКлиенте
Процедура ОбновитьСтатусГотовностиПоТаймеру()
	
	ОбновитьСтатусГотовности(Истина);
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_Статусы
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Приветствие

&НаКлиенте
Функция ПолучитьВремяСуток()
	
	ТекДата = ТекущаяДата();
	Час = Час(ТекДата) + Минута(ТекДата) / 60;
	
	Если (Час >= 22  И Час <= 24) Или (Час >= 0 И Час <= 5) Тогда
		Результат = "night";
	ИначеЕсли (Час > 5) И (Час <= 11) Тогда
		Результат = "morning";
	ИначеЕсли (Час > 11) И (Час <= 15.5) Тогда
		Результат = "day";
	Иначе
		Результат = "evening";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ИмяПользователяПолучить()
	
	Имя = СокрЛП(ПараметрыСеанса.ТекущийПользователь);
	// пытаемся убрать фамилию
	Поз = Найти(Имя, " ");
	Если Поз = 0 Тогда
	    Возврат Имя;
	Иначе
		ИО = СокрЛ(Прав(Имя, СтрДлина(Имя) - Поз));
		Если СтрДлина(ИО) < 5 Тогда
		    Возврат Имя;
		Иначе
			Возврат ИО;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПолучитьАдресСтраницыПриветствия()
	
	Адрес 		= "https://%1/Widget?time=%2&city=%3&cityen=%4&application=%5&client=%6"; // Адрес 		= "https://%1/Widget?time=%2&city=%3&cityen=%4"; sk_191129 Изменено в рамках (MOD-926)
	Город 		= "Москва";
	City  		= "Moscow";
	//(( sk_191129 Добавлено в рамках (MOD-926)
	application = "unfepf";
	client 		= СтррКонтекст.КлиентМетрикаИнформера;
	//)) sk_191129 
	
	Если ТипЗнч(СтррКонтекст.Местоположение) <> Тип("Структура") Тогда // местоположение пользователя еще не задано
		
		ПолучитьМестоположение = Ложь;
		Если ТипЗнч(СтррКонтекст.ВремяМестоположения) <> Тип("Дата") Тогда
			ПолучитьМестоположение = Истина;
		ИначеЕсли НачалоДня(СтррКонтекст.ВремяМестоположения) <> НачалоДня(ТекущаяДата()) Тогда
			ПолучитьМестоположение = Истина;
		КонецЕсли; 
		
		Если ПолучитьМестоположение Тогда
			// определяем местоположение по IP-адресу
			МодульКарты = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульКарты", СтррКонтекст);
			стррМестоположение = МодульКарты.МестоположениеПользователяПолучить();
			Если стррМестоположение.Свойство("error") Тогда
				Сообщить(НСтр("ru='Не удалось определить местоположение для информации о погоде. Причина:';uk='Неможливо визначити місце розташування для інформації про погоду. Причина:'") + Символы.ПС + стррМестоположение.error);
			Иначе
				СтррКонтекст.Местоположение = стррМестоположение;
			    СохранитьЗначениеНастройки("МестоположениеДляИнформера", СтррКонтекст.Местоположение);
			КонецЕсли; 
			СтррКонтекст.ВремяМестоположения = ТекущаяДата();
			СохранитьЗначениеНастройки("ВремяМестоположения", СтррКонтекст.ВремяМестоположения);
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если ТипЗнч(СтррКонтекст.Местоположение) = Тип("Структура") Тогда
		Попытка
			Город = СтррКонтекст.Местоположение.city_ru;
			City  = СтррКонтекст.Местоположение.city_en;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Город) И ЗначениеЗаполнено(City) Тогда
		Город = City;
	КонецЕсли; 
	
	Адрес = СтрШаблон_(Адрес, СтррКонтекст.АдресСтраницыПриветствия, ПолучитьВремяСуток(), Город, City, application, client); // Адрес = СтрШаблон_(Адрес, СтррКонтекст.АдресСтраницыПриветствия, ПолучитьВремяСуток(), Город, City); sk_191129 Изменено в рамках (MOD-926)
	Адрес = КодироватьСтрокуСервер(Адрес);
	
	Возврат Адрес;
	
КонецФункции

&НаКлиенте
Функция ПолучитьПриветствие() 
	
	ВремяСуток = ПолучитьВремяСуток();

	Если ВремяСуток = "night" Тогда
	    Приветствие = НСтр("ru='Доброй ночи';uk='Доброї ночі'");
	ИначеЕсли ВремяСуток = "morning" Тогда
		Приветствие = НСтр("ru='Доброе утро';uk='Доброго ранку'");
	ИначеЕсли ВремяСуток = "day" Тогда
		Приветствие = НСтр("ru='Добрый день';uk='Доброго дня'");
	Иначе
		Приветствие = НСтр("ru='Добрый вечер';uk='Добрий вечір'");
	КонецЕсли;
	
	Имя = ИмяПользователяПолучить();
	Если Не ПустаяСтрока(Имя) Тогда
		Приветствие = Приветствие + ", " + Имя;    
	КонецЕсли;	
	Приветствие = Приветствие + "!";
	
	Возврат Приветствие;
	
КонецФункции

&НаКлиенте
Процедура ИнформационноеПолеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиНаЗакладкуОсновныеХарактеристикиНоменклатуры" Тогда 
		
		МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
		стррПараметры = Новый Структура("Закладка,Сообщение", "ГруппаОсновные", "УказатьВыгружатьХарактеристикиНоменклатуры");
		Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", стррПараметры);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиНаЗакладкуОсновныеИспользоватьКонтрагентов" Тогда
		
		МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
		стррПараметры = Новый Структура("Закладка,Сообщение", "ГруппаОсновные", "УказатьИспользоватьКонтрагентов");
		Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", стррПараметры);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиНаЗакладкуОсновныеИспользоватьСоглашения" Тогда
		
		МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
		стррПараметры = Новый Структура("Закладка,Сообщение", "ГруппаОсновные", "УказатьИспользоватьСоглашения");
		Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", стррПараметры);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "НастройкиНоменклатуры"
		  Или НавигационнаяСсылкаФорматированнойСтроки = "CRMИМаркетинг"
		  Или НавигационнаяСсылкаФорматированнойСтроки = "Продажи" Тогда 
		  
		  ОткрытьФорму(
				"Обработка.ПанельАдминистрированияУТ.Форма." + НавигационнаяСсылкаФорматированнойСтроки,
		  		Новый Структура,
				ЭтаФорма, 
				"Обработка.ПанельАдминистрированияУТ.Форма." + НавигационнаяСсылкаФорматированнойСтроки + ".ОтдельноеОкно");
	
	КонецЕсли;
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_Приветствие
#КОнецОбласти 

// СлужебныеПроцедурыИФункции
#КонецОбласти
