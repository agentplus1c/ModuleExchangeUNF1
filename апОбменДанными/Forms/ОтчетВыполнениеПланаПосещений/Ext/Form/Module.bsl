#Область ГлобальныеПеременные

&НаКлиенте
Перем гМодульК;  // общий клиентский модуль

// ГлобальныеПеременные
#КонецОбласти


#Область СовместимостьСПлатформой_8_3_5

// Подставляет параметры в строку. Максимально возможное число параметров - 5.
// Параметры в строке задаются как %<номер параметра>. Нумерация параметров начинается с единицы.
//
// Параметры:
//  СтрокаПодстановки  - Строка - шаблон строки с параметрами (вхождениями вида "%ИмяПараметра");
//  Параметр<n>        - Строка - подставляемый параметр.
//
// Возвращаемое значение:
//  Строка   - текстовая строка с подставленными параметрами.
//
&НаКлиентеНаСервереБезКонтекста
Функция СтрШаблон_(СтрокаПодстановки,
	Параметр1, Параметр2 = Неопределено, Параметр3 = Неопределено, Параметр4 = Неопределено, Параметр5 = Неопределено)
	
	Результат = СтрЗаменить(СтрокаПодстановки, "%1", Параметр1);
	Результат = СтрЗаменить(Результат, "%2", Параметр2);
	Результат = СтрЗаменить(Результат, "%3", Параметр3);
	Результат = СтрЗаменить(Результат, "%4", Параметр4);
	Результат = СтрЗаменить(Результат, "%5", Параметр5);
	
	Возврат Результат;
	
КонецФункции
	
// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//     - для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//     Е если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//  СокращатьНепечатаемыеСимволы - Булево - сокращать непечатаемые символы по краям каждой из найденных подстрок.
//
// Возвращаемое значение:
//  Массив - массив строк.
&НаКлиентеНаСервереБезКонтекста 
Функция СтрРазделить_(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь)
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

///  Объединяет строки из массива в строку с разделителями.
//
// Параметры:
//  Массив      - Массив - массив строк которые необходимо объединить в одну строку;
//  Разделитель - Строка - любой набор символов, который будет использован в качестве разделителя.
//
// Возвращаемое значение:
//  Строка - строка с разделителями.
// 
&НаКлиентеНаСервереБезКонтекста 
Функция СтрСоединить_(Массив, Разделитель = ",", СокращатьНепечатаемыеСимволы = Ложь)

	Результат = "";
	ТекРазделитель = "";
	
	Для Индекс = 0 По Массив.ВГраница() Цикл
		
		Подстрока = Массив[Индекс];
		
		Если СокращатьНепечатаемыеСимволы Тогда
			Подстрока = СокрЛП(Подстрока);
		КонецЕсли;
		
		Если ТипЗнч(Подстрока) <> Тип("Строка") Тогда
			Подстрока = Строка(Подстрока);
		КонецЕсли;
		
		Результат = Результат + ТекРазделитель + Подстрока;
		
		Если Индекс = 0 Тогда
			ТекРазделитель = Разделитель;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// СовместимостьСПлатформой_8_3_5
#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СтррКонтекст = Новый Структура("СписокВсеВидыДокументов,ОбластьДней");
	
	СтррКонтекст.Вставить("СсылкаСписокТТ", Неопределено);  // идентификатор списка ТТ, закрепленного за выбранным агентом
	СтррКонтекст.Вставить("НаименованиеСписокТТ", ""); 		// наименование списка ТТ, закрепленного за выбранным агентом
	СтррКонтекст.Вставить("ИспользуетсяДляПланированияСписокТТ", Ложь); // признак использования в списке ТТ детализации до графика или плана посещений

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры);
	
	СтррКонтекст.Вставить("НомерВнеПлана", ТекОбъект.ПланПосещенийНомерПунктаВнеПлана()); // признак номера ТТ вне списка ТТ	
	
	СтррКонтекст.СписокВсеВидыДокументов = ТекОбъект.ПолучитьВсеВидыОбъектовДля1C("Документ");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ГруппаКоманднаяПанель.ЦветФона = СтррКонтекст.Цвета.ФонРаздела;
	
	// { Корректируем восстановленные из настроек формы значения

	Если ВидыДокументов.Количество() = 0 Тогда // если список пустой, заполняем его всеми видами документов
		ВидыДокументов = СтррКонтекст.СписокВсеВидыДокументов.Скопировать(); 	
	КонецЕсли; 
	
	Если ПериодОтчета.ДатаНачала = '00010101' Или ПериодОтчета.ДатаОкончания = '00010101' Тогда
		ПериодОтчета.ДатаНачала = НачалоМесяца(ТекущаяДата());
		ПериодОтчета.ДатаОкончания = ТекущаяДата();
	КонецЕсли; 
	
	// }
	
	ОтчетВыключитьАктуальность();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "АПНастройкиСброшены" Тогда
		
		ЭтаФорма.Модифицированность = Ложь;
		Закрыть();
		
	ИначеЕсли ИмяСобытия = "АППроверкаУникальностиЗапускаОбработкиОбмена" Тогда
		
		Если Параметр <> СтррКонтекст.ВХОбщиеПараметры Тогда // второй экземпляр обработки справшивает - уже открыта обработка или нет
			Оповестить("АПЗакрытьОбработкуОбменДанными", Параметр); // шлем событие закрытия обработки с конкретным ключем сеанса
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСкрытьОтбор(Команда)
	
	Элементы.СкрытьПараметры.Пометка = Не Элементы.СкрытьПараметры.Пометка;
	ФормаОбновитьВидимостьОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСформироватьОтчет(Команда)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ЭтаФорма.Элементы.ТДОтчет, "ФормированиеОтчета");	
	ПодключитьОбработчикОжидания("ОтчетСформироватьКлиент", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечать(Команда)
	
	Если ТДОтчет.ВысотаТаблицы = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Отчет не сформирован, сначала сформируйте отчет.';uk='Звіт не сформований, спочатку сформуйте звіт.'"));
		Возврат;
	КонецЕсли; 
	
	ТДОтчет.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТДОтчет.ПолеСлева	= 1;
    ТДОтчет.ПолеСправа	= 1;
	ТДОтчет.ПолеСверху	= 4;
	ТДОтчет.ПолеСнизу	= 1;
	
	ТДОтчет.ВерхнийКолонтитул.Выводить			 = Истина;
    ТДОтчет.РазмерКолонтитулаСверху				 = 4;
    ТДОтчет.ВерхнийКолонтитул.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
    ТДОтчет.ВерхнийКолонтитул.НачальнаяСтраница	 = 1;    
    ТДОтчет.ВерхнийКолонтитул.Шрифт				 = Новый Шрифт("Arial", 8,,Истина);
	ТДОтчет.ВерхнийКолонтитул.ТекстСправа		 = НСтр("ru = 'Стр.[&НомерСтраницы] из [&СтраницВсего]'; uk = 'Стор.[&НомерСтраницы] з [&СтраницВсего]'");
	
	ТДОтчет.Напечатать(РежимИспользованияДиалогаПечати.Использовать);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьНаКарте(Команда)
	
	Если ТДОтчет.ВысотаТаблицы = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Отчет не сформирован, сначала сформируйте отчет.';uk='Звіт не сформований, спочатку сформуйте звіт.'"));
		Возврат;
	КонецЕсли; 
	
	Расшифровка = Элементы.ТДОтчет.ТекущаяОбласть.Расшифровка;
		
	ТекОбласть = Элементы.ТДОтчет.ТекущаяОбласть;
	стррОбласть = СтррКонтекст.ОбластьДней;
	Если  ТекОбласть.Верх >= стррОбласть.Верх И ТекОбласть.Низ <= стррОбласть.Низ 
		И ТекОбласть.Лево >= стррОбласть.Лево И ТекОбласть.Право <= стррОбласть.Право
	Тогда
	    Дата = ПериодОтчета.ДатаНачала + (ТекОбласть.Лево - стррОбласть.Лево - 1) * 86400;
		ПоказатьМаршрутНаДату(Дата);
	ИначеЕсли Расшифровка = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Для просмотра данных на карте выделите контрагента или одну из ячеек дня.';uk='Для перегляду даних на карті виділіть контрагента або одну з комірок дня.'"));
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(Расшифровка) = Тип("СправочникСсылка.Контрагенты") Тогда
		ПоказатьКонтрагентовПоРасшифровке();
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("Структура") Тогда		
		//ПоказатьМаршрутПоРасшифровке();
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	МодульК().КомандаВыполнить(Команда, ЭтотОбъект)
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидыДокументовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокДляПометки = СтррКонтекст.СписокВсеВидыДокументов.Скопировать(); 
	
	Для Каждого ЭлементСписка Из СписокДляПометки Цикл 
		ЭлементСписка.Пометка = (Неопределено <> ВидыДокументов.НайтиПоЗначению(ЭлементСписка.Значение)); 
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ОповещениеОтметкиСпискаВидовДокументов", ЭтотОбъект);
	СписокДляПометки.ПоказатьОтметкуЭлементов(Оповещение, НСтр("ru='Выберите документы, включаемые в отчет';uk='Виберіть документи, що включаються до звіту'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОтметкиСпискаВидовДокументов(Список, ДополнительныеПараметры) Экспорт
	
	Если Список = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолучатель = ВидыДокументов;
	СписокПолучатель.Очистить();
	
	Для Каждого Элемент из Список Цикл
		Если Элемент.Пометка Тогда					
			СписокПолучатель.Добавить(Элемент.Значение, Элемент.Представление);
		КонецЕсли;
	КонецЦикла;
	ОтчетВыключитьАктуальность();
	
КонецПроцедуры

&НаКлиенте
Процедура АгентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	стррПараметры = Новый Структура("ПараметрыВыбора", Новый Структура("Сотрудник", Агент));
	Оповещение = Новый ОписаниеОповещения("СотрудникВыборЗавершение", ЭтотОбъект);
	МодульК().ОткрытьФормуОбработки("ВыборАгента", стррПараметры, ЭтаФорма.КлючУникальности, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.Сотрудники") Тогда
		Агент = Результат;
		ОтчетВыключитьАктуальность();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АгентОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Агент) Тогда
		МодульК().ОткрытьФормуОбработки("НастройкиАгентов");
		Оповестить("АПНастройкиАгентов_Оповещение", Новый Структура("Сотрудник", Агент));
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтчетаПриИзменении(Элемент)
	
	Если ПериодОтчета.ДатаНачала > ПериодОтчета.ДатаОкончания Тогда
		Пром = ПериодОтчета.ДатаОкончания;
		ПериодОтчета.ДатаОкончания = ПериодОтчета.ДатаНачала;
		ПериодОтчета.ДатаНачала = Пром;
	КонецЕсли; 
	ОтчетВыключитьАктуальность();
	
КонецПроцедуры

&НаКлиенте
Процедура ТДОтчетОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если Расшифровка = "СписокТТ" Тогда // нужно показать список торговых точек
		СтандартнаяОбработка = Ложь;
		СтррПараметры = Новый Структура("ID", СтррКонтекст.СсылкаСписокТТ);
		МодульК().ОткрытьФормуОбработки("СписокТорговыхТочек", СтррПараметры, СтррПараметры.ID);
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("Структура") Тогда // нужно показать документы за данный день
		СтандартнаяОбработка = Ложь;
		Список = ПолучитьСписокДокументовДляРасшифовки(Расшифровка);
		ОбработкаВыбора = Новый ОписаниеОповещения("ВыборДокументаРасшифровкиИзСписка", ЭтотОбъект, Элемент);
		ПоказатьВыборИзСписка(ОбработкаВыбора, Список, Элемент); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборДокументаРасшифровкиИзСписка(Результат, Элемент) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ТипЗнч(Результат.Значение) = Тип("Структура") Тогда
			стррДокумент = Результат.Значение;
			МодульК().ВООткрытьФорму(стррДокумент);
		Иначе	
			ПоказатьЗначение(Неопределено, Результат.Значение);
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТолькоПроведенныеДокументыПриИзменении(Элемент)
	ОтчетВыключитьАктуальность();
КонецПроцедуры

// ОбработчикиСобытийЭлементовФормы
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_Прочие

&НаКлиенте
Функция МодульК()

	Если гМодульК = Неопределено Тогда
	    гМодульК = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульОбщий", СтррКонтекст);
	КонецЕсли; 
	
	Возврат гМодульК;

КонецФункции 

// СлужебныеПроцедурыИФункции_Прочие
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_РаботаСФормой

&НаКлиенте
Процедура ФормаОбновитьВидимостьОтборов()
	
	ВидимостьОтборов = Не Элементы.СкрытьПараметры.Пометка;
	Элементы.ГруппаПараметрыОтчета.Видимость = Не Элементы.СкрытьПараметры.Пометка;
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_РаботаСФормой
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ФормированиеОтчета

// Функция возвращает структуру с основными свойствами списка ТТ закрепленного за агентом - наименование, ссылку и описание ошибки.
&НаСервере
Функция ПрочитатьИПроверитьСписокТорговыхТочекАгента(Агент)

	Возврат РеквизитФормыВЗначение("Объект").ПрочитатьИПроверитьСписокТорговыхТочекАгента(Агент);

КонецФункции

&НаКлиенте
Процедура ОтчетВыключитьАктуальность()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ЭтаФорма.Элементы.ТДОтчет, "НеАктуальность");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСформироватьКлиент()
	
	ЕстьОшибки = Ложь;
	ОчиститьСообщения();
	
	Если ПериодОтчета.ДатаНачала = '00010101' Или ПериодОтчета.ДатаОкончания = '00010101' Тогда
		Текст = НСтр("ru='Укажите период отчета.';uk='Вкажіть період звіту.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, Неопределено, "ПериодОтчета");
		ЕстьОшибки = Истина;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Агент) Тогда
		Текст = НСтр("ru='Укажите торгового агента.';uk='Вкажіть торгового агента.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, Неопределено, "Сотрудник");
		ЕстьОшибки = Истина;
	Иначе
		стррСвойстваСпискаТТ = ПрочитатьИПроверитьСписокТорговыхТочекАгента(Агент);
		Если стррСвойстваСпискаТТ.КодОшибки <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(стррСвойстваСпискаТТ.ТекстОшибки, Неопределено, "Сотрудник");
			ЕстьОшибки = Истина;
		Иначе
			ЗаполнитьЗначенияСвойств(СтррКонтекст, стррСвойстваСпискаТТ, "СсылкаСписокТТ,НаименованиеСписокТТ,ИспользуетсяДляПланированияСписокТТ");
		КонецЕсли; 
	КонецЕсли; 
	
	Если ВидыДокументов.Количество() = 0 Тогда
		Текст = НСтр("ru='Укажите виды документов для отбора в отчете.';uk='Вкажіть види документів для відбору в звіті.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, Неопределено, "ВидыДокументов");
		ЕстьОшибки = Истина;
	КонецЕсли; 
	
	Если ЕстьОшибки Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ЭтаФорма.Элементы.ТДОтчет, "НеАктуальность");
		ЭтаФорма.Элементы.ТДОтчет.ОтображениеСостояния.Текст = НСтр("ru='Ошибка в параметрах отчета.';uk='Помилка в параметрах звіту.'");
		ЭтаФорма.Элементы.ТДОтчет.Доступность = Ложь;
		Возврат;
	КонецЕсли; 
	
	// Формирование отчета. В реквизите СтррКонтекст.СсылкаСписокТТ указан идентификатор проверенного списка торговых точек агента.
	ОтчетСформироватьСервер();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ЭтаФорма.Элементы.ТДОтчет, "НеИспользовать");
	Элементы.ТДОтчет.Доступность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОтчетСформироватьСервер()

	стррПараметры = Новый Структура;
	стррПараметры.Вставить("Агент", Агент);
	стррПараметры.Вставить("Период", ПериодОтчета);
	стррПараметры.Вставить("СсылкаСписокТТ", СтррКонтекст.СсылкаСписокТТ);
	стррПараметры.Вставить("ТолькоПроведенныеДокументы", ТолькоПроведенныеДокументы);
	стррПараметры.Вставить("списокВидыДокументов", ВидыДокументов);
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");	
	ТЗ = ТекОбъект.ПолучитьПланФактПосещений(стррПараметры);
	
	ДетализацияПланИлиГрафик = СтррКонтекст.ИспользуетсяДляПланированияСписокТТ;
	
	//////////////////////// 
	// Формирование печатной формы
	
	Макет = ТекОбъект.ПолучитьМакетОбработки("ОтчетПланПосещений");
	
	мЦветаФона = Новый Массив();
	мЦветаФона.Добавить(Новый Цвет(255,255,255)); // белый - плановая ТТ
	мЦветаФона.Добавить(Новый Цвет(240,240,240)); // серый - плановая ТТ
	мЦветаФона.Добавить(Новый Цвет(253,241,165)); // светло-желтый - внеплановая ТТ
	мЦветаФона.Добавить(Новый Цвет(244,230,170)); // темно-желтый  - внеплановая ТТ
	//мЦветаФона.Добавить(Новый Цвет(204, 255, 204));	// светло-зеленый - выделенный день
	//мЦветаФона.Добавить(Новый Цвет(160, 255, 160));	// темно-зеленый  - выделенный день
	
	стррОбластьДней = Новый Структура("Лево,Верх,Право,Низ");
	
#Область ВыводШапкиОтчета
	
	Таб = ТДОтчет;
	
	Таб.Очистить();
	
	Область = Макет.ПолучитьОбласть("Заголовок|Номер");
	ПечПараметры = Область.Параметры;	
	ПечПараметры.ПечПериод = Строка(ПериодОтчета);
	ПечПараметры.ПечАгент  = Строка(Агент);
	Таб.Вывести(Область);	
	
	Таб.НачатьГруппуСтрок("Заголовок", Истина);
	
	Область = Макет.ПолучитьОбласть("ЗаголовокДетали|Номер");
	
	ПечПараметры = Область.Параметры;
	ПечПараметры.ПечВидыДокументов 		 = Строка(ВидыДокументов);
	ПечПараметры.ПечПроведенныеДокументы = ?(ТолькоПроведенныеДокументы, НСтр("ru='только проведенные';uk='тільки проведені'"), НСтр("ru='проведенные и не проведенные';uk='проведені і не проведені'"));
	ПечПараметры.ПечСписокТТ 			 = СтррКонтекст.НаименованиеСписокТТ + ?(ДетализацияПланИлиГрафик, "", " " + НСтр("ru='(без детализации до графика или плана)';uk='(Без деталізації до графіка або плану)'"));
	ПечПараметры.СписокТТ 			 	 = "СписокТТ";
	
	Таб.Вывести(Область);
	
	Таб.Присоединить(Макет.ПолучитьОбласть("ЗаголовокДетали|Клиент"));
	Таб.Присоединить(Макет.ПолучитьОбласть("ЗаголовокДетали|Адрес"));
	
	ВсегоДней = стррПараметры.ВсегоДней;
	
	стррОбластьДней.Лево  = Таб.ШиринаТаблицы;
	стррОбластьДней.Право = стррОбластьДней.Лево + ВсегоДней;
	
	Область = Макет.ПолучитьОбласть("ЗаголовокДетали|День");
	Для НомерДня = 1 По ВсегоДней Цикл
		Таб.Присоединить(Область);
	КонецЦикла;
	Таб.Присоединить(Макет.ПолучитьОбласть("ЗаголовокДетали|Всего"));
	
	Таб.ЗакончитьГруппуСтрок();
	
	ОбластьШапкиНомер = Макет.ПолучитьОбласть("Шапка|Номер");	
	Таб.Вывести(ОбластьШапкиНомер);
	
	ОбластьШапкиКлиент = Макет.ПолучитьОбласть("Шапка|Клиент");	
	Таб.Присоединить(ОбластьШапкиКлиент);
	
	ОбластьШапкиАдрес = Макет.ПолучитьОбласть("Шапка|Адрес");	
	Таб.Присоединить(ОбластьШапкиАдрес);
	
	мДниНедели = СтрРазделить_(НСтр("ru='пн,вт,ср,чт,пт,СБ,ВС';uk='пн, вт, ср, чт, пт, СБ, НД'"), ",");
	ТекДата = ПериодОтчета.ДатаНачала;
	
	ФорматДаты = ?(Месяц(ПериодОтчета.ДатаНачала) <> Месяц(ПериодОтчета.ДатаОкончания) 
					Или Год(ПериодОтчета.ДатаНачала) <> Год(ПериодОтчета.ДатаОкончания),
					"ДФ=""дд.ММ""",	"ДФ=""дд""");

	Для НомерДня = 1 По ВсегоДней Цикл
		
		НомерДняНедели = ДеньНедели(ТекДата);
		ДопОбластьШапки = Макет.ПолучитьОбласть("Шапка|" + ?(ВыделитьДень = ТекДата, "ВыделитьДень", ИмяСекцииДняНедели(НомерДняНедели)));
		ДопОбластьШапки.Параметры.ДеньНед = мДниНедели[НомерДняНедели-1];
		ДопОбластьШапки.Параметры.ДеньМес = Формат(ТекДата, ФорматДаты);
		Таб.Присоединить(ДопОбластьШапки);
		ТекДата = ТекДата + 86400;
		
	КонецЦикла;
	
	ВсегоОбластьШапки = Макет.ПолучитьОбласть("Шапка|Всего");
	Таб.Присоединить(ВсегоОбластьШапки);
	
// ВыводШапкиОтчета
#КонецОбласти 

#Область ВыводТЧОтчета

	стррОбластьДней.Верх = Таб.ВысотаТаблицы;

	ЦветКрасный = Новый Цвет(255, 0, 0);
	ЦветСиний   = Новый Цвет(0, 0, 255);

	НомерВнеПлана = стррКонтекст.НомерВнеПлана;
	
	Номер = 0; // Номер строки в таблицы в печатной форме
	
	тзИтогиДней = Неопределено;
	ОбновитьИтогиДней(ВсегоДней, тзИтогиДней);
	
	стррИтогСтроки = Новый Структура("План,Факт,ВП");
	стррДельта     = Новый Структура("План,Факт,ВП");
	
	Для Каждого СтрокаТ Из ТЗ Цикл
		
		#Область ВыводГлавныхКолонок
		/////////////////////////////////////////////////////////////////////////////////

		Номер = Номер + 1; // Номер строки таблицы в печатной форме		
		
		ЦветФона = мЦветаФона[?(СтрокаТ.НомерВПлане = НомерВнеПлана, 2, 0) + Номер % 2];
		ОбластьСтр = Макет.ПолучитьОбласть("Строка|Номер");
		ОбластьСтр.Параметры.Номер = Номер; 
		ОбластьСтр.ТекущаяОбласть.ЦветФона = ЦветФона;
		Таб.Вывести(ОбластьСтр);
				
		ОбластьСтр = Макет.ПолучитьОбласть("Строка|Клиент");		
		ТекКонтрагент = СтрокаТ.Контрагент;
		ОбластьСтр.Параметры.ПечКлиент = ТекКонтрагент;
		ОбластьСтр.Параметры.ПечКлиентРас  = ТекКонтрагент; // клиент для расшифровки
		ОбластьСтр.ТекущаяОбласть.ЦветФона = ЦветФона;
		Таб.Присоединить(ОбластьСтр);
		
		ОбластьСтр = Макет.ПолучитьОбласть("Строка|Адрес");
		ОбластьСтр.Параметры.ПечАдрес = СтрокаТ.Адрес;
		ОбластьСтр.ТекущаяОбласть.ЦветФона = ЦветФона;
		Таб.Присоединить(ОбластьСтр);
		
		// ВыводГлавныхКолонок
		#КонецОбласти 

		#Область ВыводКолонокДней
		/////////////////////////////////////////////////////////////////////////////////
		
		стррИтогСтроки.План = 0; стррИтогСтроки.Факт = 0; стррИтогСтроки.ВП = 0;
		
		ТекДата = ПериодОтчета.ДатаНачала;	
		Для НомерДня = 1 По ВсегоДней Цикл
			
			НомерДняНедели = ДеньНедели(ТекДата);
			ОбластьСтр = Макет.ПолучитьОбласть("Строка|" + ИмяСекцииДняНедели(НомерДняНедели));
			ОбластьСтр.ТекущаяОбласть.ЦветФона = ЦветФона;
			План = СтрокаТ["П" + НомерДня]; // число - порядок посещения ТТ (если включен порядок)
			Факт = ЗначениеЗаполнено(СтрокаТ["Д" + НомерДня]); // строка времени - факт создания документа для данной ТТ данным агентом
			
			стррДельта.План = 0; стррДельта.Факт = 0; стррДельта.ВП = 0;
			
			Если План <> 0 Тогда
				стррДельта.План = 1;
				Если Факт Тогда
					ОбластьСтр.Параметры.ПечРабота = "n"; // выполнено посещение по плану
					ОбластьСтр.Параметры.Док = Новый Структура("День,Контрагент", ТекДата, ТекКонтрагент); // расшифровка для отображения документов
					стррДельта.Факт = 1;
				Иначе
				    ОбластьСтр.Параметры.ПечРабота = "o"; // не выполнено посещение по плану
					ОбластьСтр.ТекущаяОбласть.ЦветТекста = ЦветКрасный; 
				КонецЕсли; 
			ИначеЕсли Факт Тогда 
				Если ДетализацияПланИлиГрафик Тогда
				    ОбластьСтр.Параметры.ПечРабота = "x"; // посещение вне плана
					ОбластьСтр.ТекущаяОбласть.ЦветТекста = ЦветСиний; 
					ОбластьСтр.Параметры.Док = Новый Структура("День,Контрагент", ТекДата, ТекКонтрагент); // расшифровка для отображения документов
					стррДельта.ВП = 1;
				Иначе // список ТТ без детализации до графика или плана - все факты считаем выполненными по плану
					Если СтрокаТ.НомерВПлане < НомерВнеПлана Тогда
						ОбластьСтр.Параметры.ПечРабота = "n"; // выполнено посещение по плану
						ОбластьСтр.Параметры.Док = Новый Структура("День,Контрагент", ТекДата, ТекКонтрагент); // расшифровка для отображения документов
						стррДельта.Факт = 1;
					Иначе
					    ОбластьСтр.Параметры.ПечРабота = "x"; // посещение вне плана
						ОбластьСтр.ТекущаяОбласть.ЦветТекста = ЦветСиний; 
						ОбластьСтр.Параметры.Док = Новый Структура("День,Контрагент", ТекДата, ТекКонтрагент); // расшифровка для отображения документов
						стррДельта.ВП = 1;
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
			//Если ВыделитьДень = ТекДата Тогда
			//	ОбластьСтр.ТекущаяОбласть.ЦветФона = мЦветаФона[4 + Номер % 2];
			//КонецЕсли; 
			Таб.Присоединить(ОбластьСтр);
			ОбновитьИтогиДней(НомерДня, тзИтогиДней, стррИтогСтроки, стррДельта);
			ТекДата = ТекДата + 86400;
			
		КонецЦикла; // Для НомерДня = 1 По ВсегоДней Цикл
		
		стррОбластьДней.Низ = Таб.ВысотаТаблицы;
		
		// ВыводКолонокДней
		#КонецОбласти

		#Область ВыводИтогаСтрок
		/////////////////////////////////////////////////////////////////////////////////
		
		ОбластьСтр = Макет.ПолучитьОбласть("Строка|Всего");
		ЗаполнитьПараметрыОбласти(ОбластьСтр.Параметры, стррИтогСтроки);
		ОбластьСтр.Области.ОблВсего.ЦветФона = ЦветФона;
		
		Таб.Присоединить(ОбластьСтр);
		
		// ВыводИтогаСтрок		
		#КонецОбласти 
		
	КонецЦикла; // Для Каждого СтрокаТ Из ТЗ Цикл

	#Область ВыводПодвалаСИтогами
	/////////////////////////////////////////////////////////////////////////////////

	Таб.Вывести(Макет.ПолучитьОбласть("Подвал|Номер"));
	Таб.Присоединить(Макет.ПолучитьОбласть("Подвал|Клиент"));	
	Таб.Присоединить(Макет.ПолучитьОбласть("Подвал|Адрес"));
	
	ТекДата = ПериодОтчета.ДатаНачала;	
	Для НомерДня = 1 По ВсегоДней Цикл
		ОбластьСтр = Макет.ПолучитьОбласть("Подвал|" + ИмяСекцииДняНедели(НомерДняНедели)); //?(ВыделитьДень = ТекДата, "ВыделитьДень", ИмяСекцииДняНедели(НомерДняНедели)));		
		ЗаполнитьПараметрыОбласти(ОбластьСтр.Параметры, тзИтогиДней[НомерДня]);
		Таб.Присоединить(ОбластьСтр);
		ТекДата = ТекДата + 86400;
	КонецЦикла;
	
	ВсегоОбластьИтогов = Макет.ПолучитьОбласть("Подвал|Всего");
	ЗаполнитьПараметрыОбласти(ВсегоОбластьИтогов.Параметры, ПолучитьОбщийИтог(тзИтогиДней));
	Таб.Присоединить(ВсегоОбластьИтогов);

	// ВыводПодвалаСИтогами
	#КонецОбласти 
	
// ВыводТЧОтчета
#КонецОбласти 

	Таб.ФиксацияСверху = 6;	
	Таб.ФиксацияСлева  = 2;
	
	СтррКонтекст.ОбластьДней = стррОбластьДней;

КонецПроцедуры // ОтчетСформироватьСервер()

// Функция обновляет итоги отчета в параметрах ТЗ (итоги по дням) и стррИтогиСтроки (итог строки).
&НаСервере
Процедура ОбновитьИтогиДней(НомерДня, ТЗ, стррИтогиСтроки = Неопределено, стррДельта  = Неопределено)
	
	Если ТЗ = Неопределено Тогда // в этом случае в параметере НомерДня передается максимальный номер дня - инициализируем таблицу итогов
		
		ТипЧисло  = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0));
		ТЗ = Новый ТаблицаЗначений;
		ТЗ.Колонки.Добавить("План", ТипЧисло);
		ТЗ.Колонки.Добавить("Факт", ТипЧисло);
		ТЗ.Колонки.Добавить("ВП", ТипЧисло);
		Для Индекс = 0 По НомерДня Цикл
			ТЗ.Добавить();
		КонецЦикла; 
		
	Иначе
		
		стррИтогиСтроки.План = стррИтогиСтроки.План + стррДельта.План;
		стррИтогиСтроки.Факт = стррИтогиСтроки.Факт + стррДельта.Факт;
		стррИтогиСтроки.ВП   = стррИтогиСтроки.ВП   + стррДельта.ВП;
		
		СтрокаТ = ТЗ[НомерДня];
		СтрокаТ.План = СтрокаТ.План + стррДельта.План;
		СтрокаТ.Факт = СтрокаТ.Факт + стррДельта.Факт;
		СтрокаТ.ВП   = СтрокаТ.ВП   + стррДельта.ВП;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура ЗаполнитьПараметрыОбласти(ПапаметрыОбласти, стррЗначения)
	
	ЗаполнитьЗначенияСвойств(ПапаметрыОбласти, стррЗначения);
	ПапаметрыОбласти.Выполнение = ?(стррЗначения.План = 0, "", Окр(стррЗначения.Факт * 100 / стррЗначения.План, 2));
	
КонецПроцедуры

// Функция возвращает общий итог для отчета (выводится в правом-нижнем углу отчета).
// Параметры:
// 		ТЗ - ТаблицаЗначений - таблица итогов по дням, подготовленная процедурой ОбновитьИтогиДней().
// 		
// Возвращаемое значение:
//   Структура   - структура с итоговыми значениями плана
//
&НаСервере 
Функция ПолучитьОбщийИтог(ТЗ)
	
	стррРезультат = Новый Структура;
	Для Каждого Колонка Из ТЗ.Колонки Цикл
		стррРезультат.Вставить(Колонка.Имя, ТЗ.Итог(Колонка.Имя));
	КонецЦикла; 
	
	Возврат стррРезультат;
	
КонецФункции

&НаСервере 
Функция ПолучитьСписокДокументовДляРасшифовки(стррРасшифровка)
	
	стррПараметры = Новый Структура;
	стррПараметры.Вставить("Агент", Агент);
	стррПараметры.Вставить("Дата", стррРасшифровка.День);
	стррПараметры.Вставить("ТолькоПроведенныеДокументы", ТолькоПроведенныеДокументы);
	стррПараметры.Вставить("списокВидыДокументов", ВидыДокументов);
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТЗ = ТекОбъект.ПолучитьФактПосещенийДляОтчета(стррПараметры);
	
	Контрагент = стррРасшифровка.Контрагент;
	
	ЕстьВиртуальныеДокументы = ТЗ.Колонки.Найти("СсылкаВДок") <> Неопределено;
	
	Список = Новый СписокЗначений;
	
	Для Каждого СтрокаТ Из ТЗ Цикл
		Если СтрокаТ.Контрагент = Контрагент Тогда
			Если ЕстьВиртуальныеДокументы И ЗначениеЗаполнено(СтрокаТ.СсылкаВДок) Тогда // виртуальный документ
				стррДокумент 	= СтрокаТ.СсылкаВДок;
				тзЖурнала 		= ТекОбъект.ВОЗагрузитьТЗ(стррДокумент.ВидДокумента, Истина); // ТЗ загружается из кэша
				стзЖурнала 		= тзЖурнала.Найти(стррДокумент.ID, "ID");
				Представление 	= ТекОбъект.ВДокПредставление(стррДокумент.ВидДокумента, стзЖурнала);
				Список.Добавить(стррДокумент, Представление);
			Иначе // обычный документ
				Список.Добавить(СтрокаТ.Ссылка);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Список;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСекцииДняНедели(НомерДняНедели)
	Если НомерДняНедели = 6 Тогда
		Возврат "Субб";
	ИначеЕсли НомерДняНедели = 7 Тогда
		Возврат "Воскр";
	Иначе
		Возврат "День";
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ВнешнийВызовСформироватьОтчет(стррПараметры) Экспорт
	
	ПериодОтчета 	= стррПараметры.Период;
	Агент      		= стррПараметры.Агент;
	ВидыДокументов 	= стррПараметры.СписокВидыДокументов.Скопировать();
	ТолькоПроведенныеДокументы = стррПараметры.ТолькоПроведенныеДокументы;
	ВыделитьДень	= стррПараметры.ВыделитьДень;
	
	ОтчетСформироватьКлиент();
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_ФормированиеОтчета
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_ИнтерактивностьОтчета

&НаКлиенте
Процедура ПоказатьМаршрутНаДату(Дата)
	
	//стррРасшифровка = Элементы.ТДОтчет.ТекущаяОбласть.Расшифровка;
	Период = Новый СтандартныйПериод(НачалоДня(Дата), КонецДня(Дата));
	стррПараметры = Новый Структура("Агент,Период,СписокВидыДокументов,ТолькоПроведенныеДокументы", 
		Агент, Период, ВидыДокументов, ТолькоПроведенныеДокументы);
	Форма = МодульК().ОткрытьФормуОбработки("ОтчетКонтрольПередвиженийАгентов");
	Форма.ВнешнийВызовПоказатьТрек(стррПараметры)
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтрагентовПоРасшифровке()
	
	ТекОбласть = Элементы.ТДОтчет.ТекущаяОбласть;
	Если ТекОбласть.Верх = ТекОбласть.Низ Тогда // выделена одна ячейка контрагента
		мКонтрагенты = Новый Массив;
		мКонтрагенты.Добавить(ТекОбласть.Расшифровка);
	Иначе // выделено несколько ячеек с контрагентами
		мКонтрагенты = ПолучитьРасшифрокиОбласти(ТекОбласть.Верх, ТекОбласть.Низ, ТекОбласть.Лево, Тип("СправочникСсылка.Контрагенты"));
	КонецЕсли; 
	Форма = МодульК().ОткрытьФормуОбработки("РедакторМеток");
	Форма.ВнешнийВызовДобавитьИПоказатьКонтрагентов(мКонтрагенты);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРасшифрокиОбласти(НачНомерСтроки, КонНомерСтроки, НомерКолонки, типЗначенияРасшифвроки)
	
	мЗначения = Новый Массив;
	Для СтрокаОтчета = НачНомерСтроки По КонНомерСтроки Цикл
		Область = ТДОтчет.ПолучитьОбласть(СтрокаОтчета, НомерКолонки, СтрокаОтчета, НомерКолонки);
		Расшифровка = Область.ТекущаяОбласть.Расшифровка;
		Если ТипЗнч(Расшифровка) = типЗначенияРасшифвроки Тогда
			мЗначения.Добавить(Расшифровка);
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат мЗначения;
	
КонецФункции

// СлужебныеПроцедурыИФункции_ИнтерактивностьОтчета
#КонецОбласти 

// СлужебныеПроцедурыИФункции
#КонецОбласти
