#Область ГлобальныеПеременные

&НаКлиенте
Перем гМодульК; // общий клиентский модуль

// ГлобальныеПеременные
#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не Параметры.Свойство("ПараметрыВыбора") Тогда
		ВызватьИсключение(НСтр("ru='Ожидается свойство ""ПараметрыВыбора"".';uk='Очікується властивість ""ПараметрыВыбора"".'"));
	КонецЕсли;
	
	СтррКонтекст =  Параметры.ПараметрыВыбора;	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры);
	СтрСвойства = "Сотрудник";
	Если Не ТекОбъект.СтруктураЕстьСвойства(СтррКонтекст, СтрСвойства) Тогда
		ВызватьИсключение(НСтр("ru='У переданной структуры должны быть свойства: ';uk='У переданої структури повинні бути властивості:'") + СтрСвойства);
	КонецЕсли;
	
	ПрочитатьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ГруппаКоманднаяПанель.ЦветФона = СтррКонтекст.Цвета.ФонРаздела;
	Элементы.ГруппаСостояние.ЦветФона 		= СтррКонтекст.Цвета.ФонСостояние;
	
	Если СтррКонтекст.Свойство("МножественныйВыбор") И СтррКонтекст.МножественныйВыбор = Истина Тогда
		ЭтаФорма.Заголовок = НСтр("ru='Выберите торговых агентов';uk='Виберіть торгових агентів'");
	КонецЕсли;
	
	ОбновитьКолонкуМУ();	
	Если ПоказатьТолькоРаботающих Тогда
		ОбновитьСписокАгентовКлиент();
	КонецЕсли; 
	ВыделитьСтрокуПоСотруднику(СтррКонтекст.Сотрудник);
	
КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	СтрокаТ = Элементы.НастройкиАгентов.ТекущиеДанные;
	Если СтрокаТ = Неопределено Тогда
		Возврат;
	Иначе
		КодСтатуса = Неопределено;
		СтррКонтекст.Свойство("КодСтатусаАгента", КодСтатуса);
		Если КодСтатуса <> Неопределено И СтрокаТ.СтатусНастроек <> КодСтатуса Тогда
			Текст = НСтр("ru='Данного агента нельзя выбрать по причине:';uk='Даного агента не можна вибрати через:'") + Символы.ПС + Элементы.ДекорацияСтатус.Заголовок;
			ПоказатьПредупреждение(Неопределено, Текст);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если СтррКонтекст.Свойство("МножественныйВыбор") И СтррКонтекст.МножественныйВыбор = Истина Тогда
		Результат = Новый Массив;
		Для каждого идСтроки Из Элементы.НастройкиАгентов.ВыделенныеСтроки Цикл
			СтрокаТ = Объект.НастройкиАгентов.НайтиПоИдентификатору(ИдСтроки);
			Результат.Добавить(СтрокаТ.Сотрудник);
		КонецЦикла; 
	Иначе
		Результат = СтрокаТ.Сотрудник;
	КонецЕсли; 
	
	Закрыть(Результат);	
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьТолькоРаботающих(Команда)
	
	ПоказатьТолькоРаботающих = Не ПоказатьТолькоРаботающих;
	ОбновитьСписокАгентовКлиент();
	
КонецПроцедуры

#Область ОбработчикиКомандФормы_НавигацияПоФормам

&НаКлиенте
Процедура КомандаПоказатьТорговыхАгентов(Команда)
	
	НастройкиАгентовОткрытьНастройки(Ложь);
	
КонецПроцедуры

// ОбработчикиКомандФормы_НавигацияПоФормам
#КонецОбласти

// ОбработчикиКомандФормы
#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТабНастройкиАгентов

&НаКлиенте
Процедура НастройкиАгентовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КомандаВыбрать(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиАгентовПриАктивизацииСтроки(Элемент)
	
	ОбновитьТекстСтатуса();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиАгентовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	НастройкиАгентовОткрытьНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиАгентовОткрытьНастройки(ДляДобавленияНового)
	
	СтрокаТ = Элементы.НастройкиАгентов.ТекущиеДанные;
	Сотрудник = ?(СтрокаТ = Неопределено, Неопределено, СтрокаТ.Сотрудник);
	МодульК().ОткрытьФормуОбработки("НастройкиАгентов", СтррКонтекст);
	стррПараметры = Новый Структура("Сотрудник,Добавить", Сотрудник, ДляДобавленияНового);
	Оповестить("АПНастройкиАгентов_Оповещение", стррПараметры);
	
	Закрыть();	
	
КонецПроцедуры

// ОбработчикиСобытийЭлементовТаблицыФормыТабНастройкиАгентов
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_Прочие

&НаКлиенте
Функция МодульК()

	Если гМодульК = Неопределено Тогда
	    гМодульК = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульОбщий", СтррКонтекст);
	КонецЕсли; 
	
	Возврат гМодульК;

КонецФункции

// СлужебныеПроцедурыИФункции_Прочие
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ХранилищеНастроек

&НаСервере
Процедура ПрочитатьНастройки()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.ВосстановитьЗначенияНастроекОбработки("НастройкиАгентов,МобильныеУстройства");
	ЗначениеВРеквизитФормы(ТекОбъект, "Объект");

КонецПроцедуры

// СлужебныеПроцедурыИФункции_ХранилищеНастроек
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ТабНастройкиАгентов

&НаКлиенте
Процедура ОбновитьКолонкуМУ()

	СтррОтбор = Новый Структура("ID");
	тзМобильныеУстройства = Объект.МобильныеУстройства;
	
	Для Каждого СтрокаТ Из Объект.НастройкиАгентов Цикл
		Если ЗначениеЗаполнено(СтрокаТ.СсылкаМУ) Тогда
			СтррОтбор.ID = СтрокаТ.СсылкаМУ;
			МассивМУ = тзМобильныеУстройства.НайтиСтроки(СтррОтбор);
			СтрокаТ.ПредставлениеМУ = ?(МассивМУ.Количество() = 0, "", МассивМУ[0].Наименование);
		Иначе
			СтрокаТ.ПредставлениеМУ = "";
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ВыделитьСтрокуПоСотруднику(Сотрудник)
	
	мСтроки = Объект.НастройкиАгентов.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
	Если мСтроки.Количество() <> 0 Тогда
		Элементы.НастройкиАгентов.ТекущаяСтрока = мСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокАгентовКлиент()
	
	ЭтаФорма.Элементы.ТабНастройкиАгентовПоказатьТолькоРаботающих.Пометка = ПоказатьТолькоРаботающих;
	
	Если ПоказатьТолькоРаботающих Тогда
		мУдаляемыеСтроки = Новый Массив;
		ТЗ = Объект.НастройкиАгентов;
		Для каждого СтрокаТ Из ТЗ Цикл
			Если СтрокаТ.СтатусНастроек <> 1 Тогда
				мУдаляемыеСтроки.Добавить(СтрокаТ.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЦикла;
		Если мУдаляемыеСтроки.Количество() <> 0 Тогда
			Для Каждого ИдСтроки Из мУдаляемыеСтроки Цикл
				ТЗ.Удалить(ТЗ.Индекс(ТЗ.НайтиПоИдентификатору(ИдСтроки)));
			КонецЦикла;
			ОбновитьТекстСтатуса();
		КонецЕсли;
	Иначе
		ПрочитатьНастройки();
		ОбновитьКолонкуМУ();
		ОбновитьТекстСтатуса();
	КонецЕсли; 

КонецПроцедуры

// СлужебныеПроцедурыИФункции_ТабНастройкиАгентов
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_СтатусыСтрокиТабНастройкиАгентов

// Процедура обновляет текст статуса в нижней части экрана и итог количества строк в таблице
&НаКлиенте
Процедура ОбновитьТекстСтатуса(СтрокаТ = Неопределено)
	
	Статус = "";
	
	Если СтрокаТ = Неопределено Тогда
		СтрокаТ = Элементы.НастройкиАгентов.ТекущиеДанные;
	КонецЕсли;
	
	Если СтрокаТ <> Неопределено Тогда
		
		Если Не ЗначениеЗаполнено(СтрокаТ.Сотрудник) Тогда
			
			Статус = Статус + НСтр("ru='Сотрудник не заполнен.';uk='Співробітник не заповнений.'") + " ";
			
		ИначеЕсли Не СтрокаТ.Работает Тогда
			
			Статус = Статус + НСтр("ru='Агент не работает (больничный, отпуск).';uk='Агент не працює (лікарняний, відпустка).'") + " ";
			
		Иначе
			
			Если Не ЗначениеЗаполнено(СтрокаТ.СсылкаМУ) Тогда
				Статус = Статус + НСтр("ru='Мобильное устройство (МУ) у агента не выбрано. Для выбора МУ перейдите в ""Настройки агентов"".';uk='Мобільний пристрій (МП) у агента не вибрано. Для вибору МП перейдіть в ""Налаштування агентів"".'") + " ";
			КонецЕсли;
		
		КонецЕсли;
		
		Если Статус = "" Тогда
			Если СтрокаТ.СтатусНастроек = 2 Тогда
				Статус = НСтр("ru='Есть критичные замечания к настройкам агента. Подробнее смотрите ""Настройки агентов"".';uk='Є критичні зауваження до налаштувань агента. Детальніше дивіться ""Налаштування агентів"".'");
			Иначе
				Статус = НСтр("ru='Настройки агента заданы.';uk='Налаштування агента задані.'");				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ДекорацияСтатус.Заголовок = Статус;
	
	Если ВсегоСтрок <> Объект.НастройкиАгентов.Количество() Тогда // для вывода количества строк в подвале
		ВсегоСтрок = Объект.НастройкиАгентов.Количество();
		Элементы.ТабНастройкиАгентовСотрудник.ТекстПодвала = ?(ВсегоСтрок = 0, "", НСтр("ru='Всего: ';uk='Всього:'") + Строка(ВсегоСтрок));
	КонецЕсли;
	
	// боремся с отображением "всех иконок" в Платформе - когда в ТабНастройкиАгентов нет строк - убираем видимость иконки статуса
	Если Элементы.ТабНастройкиАгентовСтатусНастроекИконка.Видимость <> (ВсегоСтрок > 0) Тогда
		Элементы.ТабНастройкиАгентовСтатусНастроекИконка.Видимость = (ВсегоСтрок > 0);
	КонецЕсли;
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_СтатусыСтрокиТабНастройкиАгентов
#КонецОбласти

// СлужебныеПроцедурыИФункции
#КонецОбласти